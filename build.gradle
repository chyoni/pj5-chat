plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'cwchoiit'
version = '0.0.1'

allprojects {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    apply {
        plugin('java')
        plugin('jacoco')
        plugin('org.springframework.boot')
        plugin('io.spring.dependency-management')
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy('jacocoTestReport')
    }

    jacocoTestReport {
        dependsOn('test')
        reports {
            html.required = true
        }
        finalizedBy('jacocoTestCoverageVerification')
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                includes = [
                        'cwchoiit.chat.server.service',
                        'cwchoiit.chat.server.handler.*'
                ]

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.90
                }

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.98
                }
            }
        }
    }
}
